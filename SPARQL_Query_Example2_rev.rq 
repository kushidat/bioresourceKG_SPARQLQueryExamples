PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rh: <http://rdf.rhea-db.org/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX chebi: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX brso: <http://purl.jp/bio/10/brso/>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX taxon: <http://purl.uniprot.org/taxonomy/>
SELECT DISTINCT ?plant ?ncbigene
#SELECT (COUNT(DISTINCT ?plant) AS ?count)
#SELECT (COUNT(DISTINCT ?role) AS ?count)
WHERE {
  graph <http://metadb.riken.jp/db/xsearch_plant_brso> {		
    ?plant brso:genomic_feature/brso:has_genomic_segment/rdfs:seeAlso ?ncbigene.
      FILTER REGEX(STR(?ncbigene), "http://purl.uniprot.org/geneid/")   
    }

  SERVICE <https://sparql.uniprot.org/sparql> {
    ?uniprot rdfs:seeAlso ?ncbigene.
    ?uniprot <http://purl.uniprot.org/core/enzyme> ?enzyme.
      FILTER REGEX(STR(?ncbigene), "http://purl.uniprot.org/geneid/")
    ?uniprot up:organism ?taxid .
      VALUES (?taxid) { (taxon:3702) }
    }

  graph <http://metadb.riken.jp/db/rhea> {
    ?rhea rh:ec ?enzyme.
    ?rhea rh:side ?side .
    ?side rh:contains ?participant .
    ?participant rh:compound ?compound .
    ?compound rh:chebi ?chebi .
    }

  graph <http://metadb.riken.jp/ontology/CHEBI> {
    ?chebi rdfs:subClassOf+ ?bl_1; rdfs:label ?label_chebi .
    ?bl_1 owl:someValuesFrom ?role ; rdf:type owl:Restriction ;
      owl:onProperty <http://purl.obolibrary.org/obo/RO_0000087> . # has role
    ?role rdfs:label ?label_role.
    ?chebi rdfs:label ?label_chebi.
    ?role rdfs:subClassOf* ?upper_role.
#      VALUES(?upper_role) {(chebi:67079)} #anti-inflammatory agent
    } 
}
