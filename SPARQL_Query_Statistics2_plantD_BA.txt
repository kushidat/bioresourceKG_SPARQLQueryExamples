PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rh: <http://rdf.rhea-db.org/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX chebi: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX brso: <http://purl.jp/bio/10/brso/>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX taxon: <http://purl.uniprot.org/taxonomy/>
SELECT DISTINCT ?plant ?ncbigene
#SELECT (COUNT(DISTINCT ?plant) AS ?count)
#SELECT (COUNT(DISTINCT ?activity) AS ?count)
WHERE {
  graph <http://metadb.riken.jp/db/xsearch_plant_brso> {		
    ?plant brso:genomic_feature/brso:has_genomic_segment/rdfs:seeAlso ?ncbigene.
      FILTER REGEX(STR(?ncbigene), "http://purl.uniprot.org/geneid/")  
    }

  graph <http://metadb.riken.jp/db/uniprot_ncbigene> {
    ?uniprot rdfs:seeAlso ?ncbigene.
    }
  graph <http://metadb.riken.jp/db/uniprot_enzyme> {
    ?uniprot <http://purl.uniprot.org/core/enzyme> ?enzyme.
    }

  graph <http://metadb.riken.jp/db/rhea> {
    ?rhea rh:ec ?enzyme.
    ?rhea rh:side ?side .
    ?side rh:contains ?participant .
    ?participant rh:compound ?compound .
    ?compound rh:chebi ?chebi .
    }

  graph<http://metadb.riken.jp/db/knapsackCore> {
    ?cid <http://www.w3.org/2004/02/skos/core#closeMatch> ?chebi.
    ?cid <http://purl.jp/knapsack/resource#category>|
		<http://purl.jp/knapsack/resource#function> ?activity.
#	values(?activity){("induce flowering")}
    ?cid <http://purl.jp/knapsack/resource#targetsp> ?targetsp.
    } 
}
